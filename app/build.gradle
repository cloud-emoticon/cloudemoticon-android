apply plugin: 'com.android.application'

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 38
        versionName '1.5.1'
    }

    // conditionally load keystore.properties and configure release signing config
    def keyStoresFile = rootProject.file("keystores.properties")
    if (keyStoresFile.exists()) {
        def keyStoresProps = new Properties()
        keyStoresProps.load(new FileInputStream(keyStoresFile))

        signingConfigs {
            release {
                keyAlias keyStoresProps['keyAlias']
                keyPassword keyStoresProps['keyPassword']
                storeFile file(keyStoresProps['storeFile'])
                storePassword keyStoresProps['storePassword']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    buildTypes {
        debug {
            debuggable true
            zipAlignEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix '.debug'
        }
        release {
            debuggable false
            zipAlignEnabled true
            // TODO: do not minifyEnabled otherwise EventBus dummy methods would be gone
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Support
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    // UI
    implementation 'com.hjy.library:PinnedHeaderListView:1.0.4'
    implementation 'com.melnykov:floatingactionbutton:1.3.0'
    implementation 'com.jpardogo.materialtabstrip:library:1.1.0'
    implementation('com.github.afollestad.material-dialogs:core:0.8.5.2@aar') { transitive = true }
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.mobeta.android.dslv:library:0.9.0'
    implementation 'androidx.browser:browser:1.6.0'
    // Database
    implementation 'com.github.satyan:sugar:b16e65326a'
    // Networking
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'com.loopj.android:android-async-http:1.4.10'
    implementation 'org.apache.httpcomponents:httpcore:4.4.10'
    // IO
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'commons-io:commons-io:2.4'
    // Pre-processing
    implementation 'com.hannesdorfmann.fragmentargs:annotation:4.0.0-RC1'
    implementation 'com.hannesdorfmann.fragmentargs:bundler-parceler:4.0.0-RC1'
    annotationProcessor 'com.hannesdorfmann.fragmentargs:processor:4.0.0-RC1'
    implementation "org.parceler:parceler-api:1.1.12"
    annotationProcessor "org.parceler:parceler:1.1.12"
    implementation 'pub.devrel:easypermissions:3.0.0'
    // Debug
    implementation 'com.facebook.stetho:stetho:1.3.1'
}
